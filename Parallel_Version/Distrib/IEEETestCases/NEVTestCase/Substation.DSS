! THIS FILE CONTAINS THE DEFINITION OF THE SUBBSTATION

! "SOURCEBUS" IS THE TRANSMISSION THEVINEN EQUIVALENT BUS

! DEFINE 3-WINDING TRANSFORMER
New Transformer.substation Phases=3 winding=3
~ wdg=1 bus=sourcebus         conn=wye   kv=138  kva=54000 tap=1.0 %r=0.2
~ wdg=2 bus=13kvbus.1.2.3.4   conn=wye   kv=13.2 kva=54000 tap=1.0 %r=0.2
~ wdg=3 bus=tertiary          conn=delta kv=13.2 kva=54000 tap=1.0 %r=0.6
~ XHL=12 XHT=40  XLT=26               ! %Z

! NOTE THAT WINDING 2 IS NOT CONNECTED TO GROUND (NODE 0). GROUNDING IS THROUGH
! A 0.3 OHM REACTOR AND THEN THROUGH A 2-OHM RESISTOR, WHICH MUST BE DEFINED SEPARATELY
! NODE 4 IS THE NEUTRAL OF TRANSFORMER WDG 2;  THE CONVENTION FOR THE NEUTRAL WIRES COMING
! IN FROM THE LINES IS NODE 10. THIS IS THE GROUND GRID IN THE SUBSTATION.

! TRANSFORMER NEUTRAL REACTOR DEFINITION  (A 1-PHASE R-X BRANCH CONNECTED FROM NODE 4 TO NODE 10)
New Reactor.Neutral phases=1 bus1=13kVBus.4 bus2=13kVBus.10 R=0 X=0.3   ! 

! USE A REACTOR TO DEFINE THE SUBSTATION GROUND RESISTOR FROM NODE 10 TO NODE 0 (THE ZERO VOLTAGE REFERENCE)
New Reactor.SubGround phases=1 bus1=13kVBus.10 bus2=13kvBus.0 R=2 X=0   ! 2-ohm grounding resistance
