Clear

// Power flow example from Stevenson 2nd Ed, p.219-225

New Circuit.Stevenson1
~ bus1=1 Basekv=(115 3 sqrt /)  pu=1.02  phases=1  mvasc3=1000000   1000000

set cktmodel=pos

! Lines in ohms @ 115 kV
New Line.1-2 phases=1 Bus1=1  Bus2=2  R1=13.225   X1=52.9   c1=0   Length=1
New Line.1-4 phases=1 Bus1=1  Bus2=4  R1=19.8375  X1=79.35  c1=0   Length=1
New Line.1-5 phases=1 Bus1=1  Bus2=5  R1=6.6125   X1=26.45  c1=0   Length=1
New Line.2-3 phases=1 Bus1=2  Bus2=3  R1=6.6125   X1=26.45  c1=0   Length=1
New Line.2-4 phases=1 Bus1=2  Bus2=4  R1=13.225   X1=52.9   c1=0   Length=1
New Line.3-5 phases=1 Bus1=3  Bus2=5  R1=6.6125   X1=26.45  c1=0   Length=1

New Generator.Bus3  phases=1 bus1=3   kV=(115 3 sqrt /)   kW=(100000 3 /)  model=3  Vpu=1.04 maxkvar=20000  minkvar=-15000  
~ PVFactor=0.5  ! default is 0.1; This is faster.
!  ~ debug=yes

New load.Bus2  phases=1  bus1=2  kV=(115 3 sqrt /)  kW=(60000 3 /)  kvar=(30000 3 /)  vmin=0.9
New load.Bus4  phases=1  bus1=4  kV=(115 3 sqrt /)  kW=(40000 3 /)  kvar=(10000 3 /)  vmin=0.9
New load.Bus5  phases=1  bus1=5  kV=(115 3 sqrt /)  kW=(60000 3 /)  kvar=(20000 3 /)  vmin=0.9


Set voltagebases=[115]
calcv

set maxiterations=30
solve



Show Powers MVA Elements

Show Voltage LN Nodes
show mismatch
Show convergence
Show Power MVA
