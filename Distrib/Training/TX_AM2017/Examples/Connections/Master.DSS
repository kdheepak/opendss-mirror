Clear

! Example to show behaviors of different distribution transformer connections

New Circuit.Connections

! Substation transformer
New Transformer.substation phases=3 windings=2 XHL=8
~ wdg=1 Bus=SourceBus.1.2.3 kV=115 kVA=25000 %R=.4 Conn=Delta
~ wdg=2 Bus=B1.1.2.3.0 kV=12.47 kVA=25000 %R=.4 Conn=wye

! Add a series of Default lines (1 km each) and install a transformer
New Line.Line1 Bus1=B1 Bus2=B2 Length=1 Units=km
New Line.Line2 Bus1=B2 Bus2=B3 Length=1 Units=km
New Line.Line3 Bus1=B3 Bus2=B4 Length=1 Units=km
New Line.Line4 Bus1=B4 Bus2=B5 Length=1 Units=km

! Define Transformers for each location (B2..B5)
New Transformer.T2 phases=3 windings=2 XHL=5
~ wdg=1 Bus=B2.1.2.3.0   kV=12.47 kVA=500 %R=.4 Conn=Delta
~ wdg=2 Bus=B2LV.1.2.3.0 kV=.208  kVA=500 %R=.4 Conn=wye

New Transformer.T3 phases=3 windings=2 XHL=7.5
~ wdg=1 Bus=B3.1.2.3.0   kV=12.47 kVA=10000 %R=.4 Conn=Wye
~ wdg=2 Bus=B3LV.1.2.3.4 kV=.585  kVA=10000 %R=.4 Conn=wye

New Transformer.T4 phases=3 windings=3 XHL=5  XHT=90   XLT=    75
~ wdg=1 Bus=B4.1.2.3.444   kV=12.47 kVA=500 %R=.4 Conn=Wye  ! Ungrounded
~ wdg=2 Bus=B4LV.1.2.3.0 kV=.208  kVA=500 %R=.4 Conn=wye
~ wdg=3 Bus=Dummy.1.2.3 kV=.208  kVA=500 %R=.4 Conn=Delta



New Transformer.T5 phases=3 windings=2 XHL=5 
~ wdg=1 Bus=B5.1.2.3.0   kV=12.47 kVA=500 %R=.4 Conn=Wye 
~ wdg=2 Bus=B5LV.1.2.3.0 kV=.208  kVA=500 %R=.4 Conn=Delta

! Add both 3-phase and 1-phase loads at each location
New Load.Load2 Bus1=B2LV.1.2.3.0 phases=3 kV=0.208 kW=400 PF=0.95 conn=wye
! New Load.Load3 Bus1=B3LV.1.2.3.0 phases=3 kV=0.208 kW=400 PF=0.95 conn=wye
! New Load.Load4 Bus1=B4LV.1.2.3.0 phases=3 kV=0.208 kW=400 PF=0.95 conn=wye
New Load.Load5 Bus1=B5LV.1.2.3.0 phases=3 kV=0.208 kW=400 PF=0.95 conn=delta

New Load.Load2-1ph Bus1=B2LV.1.0 phases=1 kV=0.120 kW=20 PF=0.98 conn=wye
!New Load.Load3-1ph Bus1=B3LV.1.0 phases=1 kV=0.120 kW=20 PF=0.98 conn=wye
New Load.Load4-1ph Bus1=B4LV.1.0 phases=1 kV=0.120 kW=20 PF=0.98 conn=wye
New Load.Load5-1ph Bus1=B5LV.1.2 phases=1 kV=0.208 kW=20 PF=0.98 conn=delta

New Generator.B3  Bus1=B3  Conn=delta  kV=.585  kW=10000 PF=1


Set voltagebases=[115, 12.47  .585 .208]
calcvoltagebases

Solve
Show V LN Nodes
Show currents element
Show powers kVA element