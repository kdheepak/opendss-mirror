clear
new circuit.dummy

set EarthModel=Carson // for comparison to Kersting's text book
                      // other choices are Deri and FullCarson

new WireData.CU_1/0 NormAmps=100 DIAM=0.368  GMRac=0.13356 Rac=0.607 Runits=mi Radunits=in gmrunits=in

new TSData.TS_1/0   NormAmps=165 DIAM=0.368  GMRac=0.13320 Rac=0.97  Runits=mi Radunits=in gmrunits=in
~                   EpsR=2.3 Ins=0.220 DiaIns=0.82 DiaCable=1.06 DiaShield=0.88 TapeLayer=0.005 TapeLap=20

new CNData.CN_250   NormAmps=260 DIAM=0.567  GMRac=0.20520 Rac=0.41  Runits=mi Radunits=in gmrunits=in
~                   EpsR=2.3 Ins=0.220 DiaIns=1.06 DiaCable=1.29 k=13 DiaStrand=0.0641 GmrStrand=0.02496 Rstrand=14.8722

new CNData.CN_1/0AL NormAmps=265 DIAM=0.368  GMRac=0.13320 Rac=0.9580000  Runits=mi Radunits=in gmrunits=in
~                   EpsR=2.3 Ins=0.345 DiaIns=1.0625 DiaCable=1.29 k=6 DiaStrand=0.0641 GmrStrand=0.02496 Rstrand=14.8722

! [cables]
! Dins is outside diameter over insulation
! Dscr is diameter over insulation screen, = I.D. of shield or CN layer
! mils is insulation thickness, determines insulation I.D. for capacitance
! id;               PhWire;  CNWire;  Dins   Dscr  mils #CN   k
! 2(7x) 1/3 CN;        #2 AA;  #14 CU;  0.78   0.85   220   6 2.3
! 1/0(19x) 1/3 CN;    1/0 AA;  #14 CU;  0.85   0.93   220   6 2.3
! 2/0(19x) 1/3 CN;    2/0 AA;  #14 CU;  0.90   0.97   220   7 2.3
! 250(37x) 1/3 CN;    250 AA;  #14 CU;  1.06   1.16   220  13 2.3
! 500(37x) 1/3 CN;    500 AA;  #12 CU;  1.29   1.39   220  16 2.3
! 1000(61x) 1/3 CN;  1000 AA;  #10 CU;  1.64   1.77   220  20 2.3
! 1/0 TS;             1/0 AA; 5-mil TS; 0.82   0.88   220   0 2.3

new LineSpacing.515 nconds=3 nphases=3 units=ft x=[-0.5 0.0 0.5] h=[-4 -4 -4]
new LineSpacing.520 nconds=2 nphases=1 units=ft x=[0.0 0.0833] h=[-4 -4]

! IEEE 13 System

new LineGeometry.606 nconds=3 nphases=3 reduce=y
~ cond=1 cncable=CN_250 x=-0.5 h=-4 units=ft
~ cond=2 cncable=CN_250 x= 0.0 h=-4 units=ft
~ cond=3 cncable=CN_250 x= 0.5 h=-4 units=ft

new LineGeometry.607 nconds=2 nphases=1 reduce=y
~ cond=1 tscable=TS_1/0 x= 0.0000 h=-4 units=ft
~ cond=2 wire   =CU_1/0 x= 0.2500 h=-4 units=ft

new LineGeometry.607a nconds=2 nphases=1 reduce=y
~ cond=1 cncable=CN_1/0AL x= 0.0000 h=-4 units=ft
~ cond=2 wire   =CU_1/0   x= 0.2500 h=-4 units=ft

show lineconstants freq=60 units=kft
